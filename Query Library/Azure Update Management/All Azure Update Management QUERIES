
// Table of Needed Updates for Windows Computers as of the last scan, by Computer
Update
| where OSType != "Linux" and UpdateState == "Needed"
| summarize hint.strategy=partitioned arg_max(TimeGenerated, *) by Computer, SourceComputerId, UpdateID
| project Computer, Product, Classification, MSRCSeverity, KBID, Title, RebootBehavior
| sort by Computer asc

// Table of Needed Updates for Linux Computers as of the last scan, by Computer
Update
| where OSType == "Linux" and UpdateState == "Needed"
| summarize hint.strategy=partitioned arg_max(TimeGenerated, *) by Computer, SourceComputerId, UpdateID
| project Computer, OSName, Classification, PackageSeverity, BulletinID, CVENumbers
| sort by Computer asc

// Table of Needed Updates for All (Windows and Linux) Computers as of the last scan, by Computer
Update
| where UpdateState == "Needed"
| summarize hint.strategy=partitioned arg_max(TimeGenerated, *) by Computer, SourceComputerId, UpdateID
| extend OSType = iff(isempty(OSType), "Windows", OSType)
| extend UpdateFor = iff(isempty(OSName), Product, OSName)
| extend Severity = iff(isempty(PackageSeverity), MSRCSeverity, PackageSeverity)
| extend ID = iff(isempty(BulletinID), iff(isnotempty(KBID),strcat("KB: ",KBID),""), strcat("Bulletin: ", BulletinID))
| extend Description = iff(isempty(CVENumbers), Title, CVENumbers)
| project Computer, OSType, UpdateFor , Classification, Severity, ID, Description, RebootBehavior
| sort by Computer asc

// Table of Needed Operating System Updates for All (Windows and Linux) Computers as of the last scan, by Computer
Update
| where UpdateState == "Needed"
| where Product startswith "Windows Server" or OSName != ""
| summarize hint.strategy=partitioned arg_max(TimeGenerated, *) by Computer, SourceComputerId, UpdateID
| extend OSType = iff(isempty(OSType), "Windows", OSType)
| extend UpdateFor = iff(isempty(OSName), Product, OSName)
| extend Severity = iff(isempty(PackageSeverity), MSRCSeverity, PackageSeverity)
| extend ID = iff(isempty(BulletinID), iff(isnotempty(KBID),strcat("KB: ",KBID),""), strcat("Bulletin: ", BulletinID))
| extend Description = iff(isempty(CVENumbers), Title, CVENumbers)
| project Computer, OSType, UpdateFor , Classification, Severity, ID, Description, RebootBehavior
| sort by Computer asc

// Pie Chart of Needed Operating System Updates for All (Windows and Linux) Computers as of the last scan
// Before saving or pinning, enable the "Split By" values of your preference to make the visulaization more impactful
Update
| where UpdateState == "Needed"
| where Product startswith "Windows Server" or OSName != ""
| summarize hint.strategy=partitioned arg_max(TimeGenerated, *) by Computer, SourceComputerId, UpdateID
| extend OSType = iff(isempty(OSType), "Windows", OSType)
| extend UpdateFor = iff(isempty(OSName), Product, OSName)
| extend Severity = iff(isempty(PackageSeverity), MSRCSeverity, PackageSeverity)
| extend ID = iff(isempty(BulletinID), iff(isnotempty(KBID),strcat("KB: ",KBID),""), strcat("Bulletin: ", BulletinID))
| extend Description = iff(isempty(CVENumbers), Title, CVENumbers)
| project Computer, OSType, UpdateFor , Classification, Severity, ID, Description, RebootBehavior
| summarize count() by OSType, UpdateFor , Classification, Severity, ID, Computer
| render piechart
