// ALL Azure Update Management QUERIES

// Table: Needed Updates for Windows Computers
Update
| where OSType != "Linux" and UpdateState == "Needed"
| summarize hint.strategy=partitioned arg_max(TimeGenerated, *) by Computer, SourceComputerId, UpdateID
| project Computer, Product, Classification, MSRCSeverity, KBID, Title, RebootBehavior
| sort by Computer asc

// Table: Needed Updates for Linux Computers
Update
| where OSType == "Linux" and UpdateState == "Needed"
| summarize hint.strategy=partitioned arg_max(TimeGenerated, *) by Computer, SourceComputerId, UpdateID
| project Computer, OSName, Classification, PackageSeverity, BulletinID, CVENumbers
| sort by Computer asc

// Table: Needed Updates for All Computers (Cross-Platform)
Update
| where UpdateState == "Needed"
| summarize hint.strategy=partitioned arg_max(TimeGenerated, *) by Computer, SourceComputerId, UpdateID
| extend OSType = iff(isempty(OSType), "Windows", OSType)
| extend UpdateFor = iff(isempty(OSName), Product, OSName)
| extend Severity = iff(isempty(PackageSeverity), MSRCSeverity, PackageSeverity)
| extend ID = iff(isempty(BulletinID), iff(isnotempty(KBID),strcat("KB: ",KBID),""), strcat("Bulletin: ", BulletinID))
| extend Description = iff(isempty(CVENumbers), Title, CVENumbers)
| project Computer, OSType, UpdateFor , Classification, Severity, ID, Description, RebootBehavior
| sort by Computer asc

// Table: Needed OS Updates for All Computers (Cross-Platform)
Update
| where UpdateState == "Needed"
| where Product startswith "Windows Server" or OSName != ""
| summarize hint.strategy=partitioned arg_max(TimeGenerated, *) by Computer, SourceComputerId, UpdateID
| extend OSType = iff(isempty(OSType), "Windows", OSType)
| extend UpdateFor = iff(isempty(OSName), Product, OSName)
| extend Severity = iff(isempty(PackageSeverity), MSRCSeverity, PackageSeverity)
| extend ID = iff(isempty(BulletinID), iff(isnotempty(KBID),strcat("KB: ",KBID),""), strcat("Bulletin: ", BulletinID))
| extend Description = iff(isempty(CVENumbers), Title, CVENumbers)
| project Computer, OSType, UpdateFor , Classification, Severity, ID, Description, RebootBehavior
| sort by Computer asc

// PieChart: Needed OS Updates for All Computers (Cross-Platform)
Update
| where UpdateState == "Needed"
| where Product startswith "Windows Server" or OSName != ""
| summarize hint.strategy=partitioned arg_max(TimeGenerated, *) by Computer, SourceComputerId, UpdateID
| extend OSType = iff(isempty(OSType), "Windows", OSType)
| extend UpdateFor = iff(isempty(OSName), Product, OSName)
| extend Severity = iff(isempty(PackageSeverity), MSRCSeverity, PackageSeverity)
| extend ID = iff(isempty(BulletinID), iff(isnotempty(KBID),strcat("KB: ",KBID),""), strcat("Bulletin: ", BulletinID))
| extend Description = iff(isempty(CVENumbers), Title, CVENumbers)
| project Computer, OSType, UpdateFor , Classification, Severity, ID, Description, RebootBehavior
| summarize count() by OSType, UpdateFor , Classification, Severity, ID, Computer
| render piechart
