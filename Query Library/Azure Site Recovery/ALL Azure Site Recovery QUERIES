// ALL Azure Site Recovery QUERIES

// LineChart: Trend of Averaged Churn-Upload Delta
AzureDiagnostics
| where Category == "AzureSiteRecoveryProtectedDiskDataChurn"
| summarize DataChurnKB = avg(toint(Value_s)/1024) by TimeGenerated, InstanceName_s, Resource, ResourceGroup
| join kind = leftouter (
  AzureDiagnostics
  | where Category == "AzureSiteRecoveryReplicationDataUploadRate"
  | summarize DataUploadKB = avg(toint(Value_s)/1024) by TimeGenerated, InstanceName_s, Resource, ResourceGroup
) on TimeGenerated, InstanceName_s
| extend ComputerName = extract("(.*?):",1, InstanceName_s)
| extend VaultName = Resource
| extend VaultResourceGroup = ResourceGroup
| summarize ["Churn Minus Upload Delta (KB)"] = sum(DataChurnKB-DataUploadKB) by TimeGenerated, ComputerName, VaultName, VaultResourceGroup
| render timechart

// LineChart: Trend of Raw Churn-Upload Delta
AzureDiagnostics
| where Category == "AzureSiteRecoveryProtectedDiskDataChurn"
| extend DataChurnKB = toint(Value_s)/1024
| project DataChurnKB, TimeGenerated, InstanceName_s, Resource, ResourceGroup
| join kind = leftouter (
  AzureDiagnostics
  | where Category == "AzureSiteRecoveryReplicationDataUploadRate"
  | extend DataUploadKB = toint(Value_s)/1024
  | project DataUploadKB, TimeGenerated, InstanceName_s, Resource, ResourceGroup
) on InstanceName_s
| extend ComputerName = extract("(.*?):",1, InstanceName_s)
| extend VaultName = Resource
| extend VaultResourceGroup = ResourceGroup
| extend ["Churn Minus Upload Delta (KB)"] = (DataChurnKB-DataUploadKB)
| project ["Churn Minus Upload Delta (KB)"], TimeGenerated, ComputerName, VaultName, VaultResourceGroup
| render timechart

// LineChart: Trend of Average Disk Data Churn
AzureDiagnostics
| where Category == "AzureSiteRecoveryProtectedDiskDataChurn"
| extend ComputerName = extract("(.*?):",1, InstanceName_s)
| extend VaultName = Resource
| extend VaultResourceGroup = ResourceGroup
| summarize DataChurnKB = avg(toint(Value_s)/1024) by TimeGenerated, ComputerName, VaultName, VaultResourceGroup
| render timechart

// PieChart: Proportion of Average Disk Data Churn
AzureDiagnostics
| where Category == "AzureSiteRecoveryProtectedDiskDataChurn"
| extend ComputerName = extract("(.*?):",1, InstanceName_s)
| extend VaultName = Resource
| extend VaultResourceGroup = ResourceGroup
| summarize DataChurnKB = ave(toint(Value_s)/1024) by TimeGenerated, ComputerName, VaultName, VaultResourceGroup
| render piechart

// Table: Failed Site Recovery Jobs
AzureDiagnostics
| where Category == "AzureSiteRecoveryJobs"
| where ResultType == "Failed"
| extend ComputerName = affectedResourceName_s
| extend ComputerType = affectedResourceType_s
| extend VaultName = Resource
| extend VaultResourceGroup = ResourceGroup
| summarize FailureCount = count(ResultType) by ComputerName, ComputerType, SourceSystem, OperationName, VaultName, VaultResourceGroup
| sort by ComputerName asc, OperationName asc

// Table: Failover Health Errors
AzureDiagnostics
| where Category == "AzureSiteRecoveryReplicatedItems"
| where failoverHealth_s != "Normal"
| summarize hint.strategy=partitioned arg_max(TimeGenerated, *) by name_s
| mvexpand d = parsejson(failoverHealthErrors_s)
| extend errorCategory = tostring(d.errorCategory)
| extend errorCode = tostring(d.errorCode)
| extend errorLevel = tostring(d.errorLevel)
| extend errorMessage = tostring(d.errorMessage)
| extend creationTime = tostring(d.creationTime)
| extend possibleCauses = tostring(d.possibleCauses)
| extend recommendation = tostring(d.recommendation)
| project ComputerName = name_s, errorCategory, errorCode, errorLevel, errorMessage, creationTime, recommendation
| sort by errorLevel asc, ComputerName asc

// Table: Protected Item Inventory
AzureDiagnostics
| where Category == "AzureSiteRecoveryReplicatedItems"
| summarize hint.strategy=partitioned arg_max(TimeGenerated, *) by name_s
| project name_s, osFamily_s, itemType_s, activeLocation_s, primaryFabricType_s, primaryFabricName_s, recoveryFabricType_s, recoveryFabricName_s, protectionState_s, policyName_s

// Table: Replication Health Errors
AzureDiagnostics
| where Category == "AzureSiteRecoveryReplicatedItems"
| where replicationHealth_s != "Normal"
| summarize hint.strategy=partitioned arg_max(TimeGenerated, *) by name_s
| mvexpand d = parsejson(replicationHealthErrors_s)
| extend errorCategory = tostring(d.errorCategory)
| extend errorCode = tostring(d.errorCode)
| extend errorLevel = tostring(d.errorLevel)
| extend errorMessage = tostring(d.errorMessage)
| extend creationTime = tostring(d.creationTime)
| extend possibleCauses = tostring(d.possibleCauses)
| extend recommendation = tostring(d.recommendation)
| project ComputerName = name_s, errorCategory, errorCode, errorLevel, errorMessage, creationTime, possibleCauses, recommendation
| sort by errorLevel asc, ComputerName asc

