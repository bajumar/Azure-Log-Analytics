
// Table of Event Log Sources and volume ingested, by Event Log and Source
Event
| summarize Events = count() by EventLog, Source
| sort by EventLog asc, Source asc, Events

// Table of Failed Update Attempts, by Computer and Description
Event
| where EventLog == "System" and Source == "Microsoft-Windows-WindowsUpdateClient" and EventLevelName == "Error"
| extend Description = extract("(<Param>(.*?)</Param>){2}",2,ParameterXml)
| extend UpdateGUID = extract("(<Param>(.*?)</Param>){5}",2,ParameterXml)
| extend KBID = extract(@"KB(\d+)",1,ParameterXml)
| project Computer, Description, TimeGenerated, KBID, UpdateGUID
| sort by Computer asc, Description asc

// Bar Chart of Failed Update Attempts, by Computer and KBID
Event
| where EventLog == "System" and Source == "Microsoft-Windows-WindowsUpdateClient" and EventLevelName == "Error"
| extend Description = extract("(<Param>(.*?)</Param>){2}",2,ParameterXml)
| extend UpdateGUID = extract("(<Param>(.*?)</Param>){5}",2,ParameterXml)
| extend KBID = extract(@"KB(\d+)",1,ParameterXml)
| summarize Attempts = count() by Computer, KBID, Description
| render barchart kind = unstacked


